(*************************************************************
 *                                                           *
 *  Cryptographic protocol verifier                          *
 *                                                           *
 *  Bruno Blanchet, Vincent Cheval, and Marc Sylvestre       *
 *                                                           *
 *  Copyright (C) INRIA, CNRS 2000-2018                      *
 *                                                           *
 *************************************************************)

(*

    This program is free software; you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation; either version 2 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details (in file LICENSE).

    You should have received a copy of the GNU General Public License
    along with this program; if not, write to the Free Software
    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA

*)
(* Denning-Sacco
   In this model, I separate the keys for encryption and the
   keys for signature, which complicates the model. *)

free c: channel.

type host.
type key.
type pkey.
type skey.
type spkey.
type sskey.

(* Probabilistic public key encryption *)

type seed.
fun pk(skey): pkey.
fun internal_encrypt(bitstring, pkey, seed): bitstring.
reduc forall x: bitstring, y: skey, r: seed; 
        decrypt(internal_encrypt(x,pk(y),r),y) = x.
letfun encrypt(x: bitstring, y:pkey) = new r: seed; internal_encrypt(x,y,r).

(* Signatures *)

fun spk(sskey): spkey.
fun sign(bitstring, sskey): bitstring.
reduc forall m: bitstring, k: sskey; getmess(sign(m,k)) = m.
reduc forall m: bitstring, k: sskey; checksign(sign(m,k), spk(k)) = m.

(* Probabilistic shared key encryption *)

type sseed.
fun internal_sencrypt(bitstring,key,sseed): bitstring.
reduc forall x: bitstring, y: key, r: sseed; sdecrypt(internal_sencrypt(x,y,r),y) = x.
letfun sencrypt(x: bitstring, y: key) = new r: sseed; internal_sencrypt(x,y,r).

(* Secrecy assumptions *)

not attacker(new skA).
not attacker(new skB).

(* Queries *)

const secrcst, ok, error, n2: bitstring.

free secretA1, secretA2, secretB: bitstring [private].
noninterf secretA1 among (ok, n2), secretA2 among (error, n2), secretB.

(* Initiator *)

let processInitiator(skA: sskey, pkA: spkey, pkB: pkey) =
        in(c, pk2: pkey);
	new k: key;
	out(c, encrypt(sign((pkA, pk2, k), skA), pk2));
	if pk2 = pkB then
	in(c, m: bitstring);
	let (=secrcst, s:bitstring) = sdecrypt(m,k) in
	(
	out(c, sencrypt(secretA1, k)) (* secretA1 takes values "ok" or n2 *)
	)
	else
	(
	out(c, sencrypt(secretA2, k)) (* secretA2 takes values "error" or n2 *)
	).

(* Responder *)

let processResponder(skB: skey, pkB: pkey, pkA: spkey) =
	in(c, km: bitstring);
	let ks = decrypt(km,skB) in
	let (=pkA, =pkB, k:key) = checksign(ks, pkA) in
	out(c, sencrypt((secrcst, secretB), k)).

(* Main process *)

process new skA: sskey; 
	let pkA = spk(skA) in
        out(c, pkA);
        new skB: skey; 
	let pkB = pk(skB) in
        out(c, pkB);
	((!processInitiator(skA, pkA, pkB)) | 
	 (!processResponder(skB, pkB, pkA)))

