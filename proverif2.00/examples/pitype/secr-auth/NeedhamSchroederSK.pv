(*************************************************************
 *                                                           *
 *  Cryptographic protocol verifier                          *
 *                                                           *
 *  Bruno Blanchet, Vincent Cheval, and Marc Sylvestre       *
 *                                                           *
 *  Copyright (C) INRIA, CNRS 2000-2018                      *
 *                                                           *
 *************************************************************)

(*

    This program is free software; you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation; either version 2 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details (in file LICENSE).

    You should have received a copy of the GNU General Public License
    along with this program; if not, write to the Free Software
    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA

*)
(* Needham-Schroeder shared key *)

free c: channel.

type key.
type host.
type nonce.
type tag.

(* Shared key encryption *)

fun encrypt(bitstring,key): bitstring.
reduc forall x: bitstring, y: key; decrypt(encrypt(x,y),y) = x.

(* Secrecy assumptions *)

not attacker(new Kas).
not attacker(new Kbs).

(* 2 honest host names A and B *)

free A, B: host.

(* the table host names/keys 
   The key table consists of pairs 
   (host, key shared between the host and the server) *)
table keys(host, key).

(* Queries *)

query attacker(new secretA);
      attacker(new secretB).

event endAparam(host,host).
event endBparam(host,host).
event beginAparam(host,host).
event beginBparam(host,host).
event endAkey(host, host, key).
event beginAkey(host, host, key).
event endBkey(host, host, key).
event beginBkey(host, host, key).

query x: host, y: host; inj-event(endAparam(x,y)) ==> inj-event(beginAparam(x,y)).
query x: host, y: host, z: key; inj-event(endAkey(x,y,z)) ==> inj-event(beginAkey(x,y,z)).
query x: host, y: host; inj-event(endBparam(x,y)) ==> inj-event(beginBparam(x,y)).
query x: host, y: host, z: key; inj-event(endBkey(x,y,z)) ==> inj-event(beginBkey(x,y,z)).

(* constants 0 and 1 *)

const c0: tag [data].
const c1: tag [data].


(* Role of the initiator with identity xA and key kas shared with S *)

let processInitiator =
	(* Session secret *)
	new secretA: bitstring;
        (* The attacker starts the initiator by choosing identity xA.
	   We check that xA is honest (i.e. is A or B)
	   and get its corresponding key.
	*)
        in(c, (xA: host, h: host));
	if xA = A || xA = B then
	get keys(=xA, kas) in
	(* Real start of the role *)
	event beginBparam(xA,h);
	new Na: nonce;
	out(c,(xA, h, Na));
	in(c,m2: bitstring);
	let (=Na, =h, k: key, m: bitstring) = decrypt(m2, kas) in
	event beginBkey(xA, h, k);
	out(c,m);
	in(c, m3: bitstring);
	let (=c0, n: nonce) = decrypt(m3, k) in
	out(c, encrypt((c1, n), k));
	if h = B || h = A then
	event endAparam(xA, h);
	event endAkey(xA, h, k);
	out(c, encrypt(secretA, k)).

(* Role of the responder with identity xB and key kbs shared with S *)
                         
let processResponder =
	(* Session secret *)
	new secretB: bitstring;
        (* The attacker starts the responder by choosing identity xB.
	   We check that xB is honest (i.e. is A or B)
	   and get its corresponding key. *)
        in(c, xB: host);
	if xB = A || xB = B then
	get keys(=xB, kbs) in
	(* Real start of the role *)
	in(c,m4: bitstring);
	let (k: key,h: host) = decrypt(m4,kbs) in
	event beginAparam(h, xB);
	event beginAkey(h, xB, k);
	new Nb: nonce;
	out(c, encrypt((c0, Nb), k));
	in(c,m5: bitstring);
	let (=c1, =Nb) = decrypt(m5,k) in
	if h = A || h = B then
	event endBparam(h, xB);
	event endBkey(h, xB, k);
	out(c, encrypt(secretB, k)).

(* Server *)

let processS =
	in(c, (h1: host,h2: host,n: nonce));
	get keys(=h1, ks1) in
	get keys(=h2, ks2) in
	new k: key;
	out(c, encrypt((n, h2, k, encrypt((k,h1),ks2)), ks1)).

(* Key registration *)

let processK =
        in(c, (h: host, k: key));
        if h <> A && h <> B then insert keys(h,k).

(* Start process *)

process
	new Kas: key; new Kbs: key;
	insert keys(A, Kas);
	insert keys(B, Kbs);
	(
          (* Launch an unbounded number of sessions of the initiator *)
          (!processInitiator) | 
          (* Launch an unbounded number of sessions of the responder *)
          (!processResponder) |
          (* Launch an unbounded number of sessions of the server *)
          (!processS) |
          (* Key registration process *)
	  (!processK)
        )

